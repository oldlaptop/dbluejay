package require Tk 8.5

package require snit 2.2
package require tdbc

namespace eval dbluejay {

# Megawidget for browsing a single result set in a scrollable flat multi-column
# ttk::treeview.
snit::widget rstable {
	hulltype ttk::frame

	delegate option * to hull
	delegate method * to hull

	component table   ;# ttk::treeview
	component vscroll ;# ttk::scrollbar -orient vertical
	component hscroll ;# ttk::scrollbar -orient horizontal

	# TDBC resultset object to browse.
	option -rs -readonly yes -default {}

	# Number of rows to fetch from the result set at once; -chunksize rows
	# are read from the result set every time the scrollbar bottoms out,
	# until there are no more rows to read. inf is legal and behaves as you
	# would expect (fetching all rows at once at construction time).
	option -chunksize -default 256

	constructor {args} {
		$self configurelist $args
		if {[$self cget -rs] eq {}} {
			return -code error "-rs is not optional"
		}

		install table using ttk::treeview $win.table -columns [
			[$self cget -rs] columns
		] -show headings
		install vscroll using ttk::scrollbar $win.vscroll \
			-orient vertical \
			-command [mymethod Vscroll_yview]
		install hscroll using ttk::scrollbar $win.hscroll \
			-orient horizontal \
			-command [list $table xview]

		$table configure -yscrollcommand [
			mymethod Vscroll_set
		] -xscrollcommand [
			list $hscroll set
		]

		foreach col [[$self cget -rs] columns] {
			$table heading $col -text $col
		}

		grid $table   $vscroll -sticky ns
		grid $hscroll x        -sticky ew

		grid configure $table -sticky nsew
		grid rowconfigure $win 0 -weight 1
		grid columnconfigure $win 0 -weight 1

		$self read [$self cget -chunksize]
	}

	# Read and display $chunksize rows from the result set.
	method read {chunksize} {
		for {set index 0} {
			$index < $chunksize && [[$self cget -rs] nextlist row]
		} {incr index} {
			$table insert {} end -values $row
		}
	}

	# Utility method for the two Vscroll_ methods below, reading -chunksize
	# rows from the result set if the scrollbar seems bottomed-out-ish.
	method Check_read {} {
		if {[lindex [$vscroll get] 1] > 0.95} {
			$self read [$self cget -chunksize]
		}
	}

	# Assigned to the $vscroll as its -command, functions as a hook to
	# ensure the [read] method is called if needed
	method Vscroll_yview {args} {
		$table yview {*}$args
		$self Check_read
	}

	# Assigned to $table as its -yscrollcommand; this one functions as a
	# hook to ensure the [read] method is called if needed too
	method Vscroll_set {args} {
		$vscroll set {*}$args
		$self Check_read
	}
}

# Megawidget for browsing all the result sets generated by a multi-statement
# SQL script in a cargocult::cnotebook.
snit::widgetadaptor rspager {
	delegate method * to hull
	delegate option * to hull

	# TDBC handle on which to execute SQL scripts
	option -db -default {apply {{args} {
		return -code error "please supply a tdbc handle"
	}}}

	# SQL script to execute
	option -sql -default {} -configuremethod Set_sql
	method Set_sql {opt val} {
		set options($opt) $val
		after idle [list $self update]
	}

	# Bound parameters to apply to all statements in the script
	option -params -default {}

	# Number of rows to fetch from each result set at once, passed directly
	# to dbluejay::rstable
	option -chunksize -default 256

	# Used to serialize dbluejay::rstable widgets
	variable rs_serial 0

	constructor {args} {
		installhull using cargocult::cnotebook -height 300

		$self configurelist $args
	}

	# Clear out our tabs and replace them with newer, better ones that
	# reflect the current state of the database and the current state of our
	# -sql script.
	method update {} {
		$self clear

		set sql_acc {}
		foreach token [tdbc::tokenize [$self cget -sql]] {
			if {$token eq {;}} {
				$self Execute $sql_acc
				set sql_acc {}
			} else {
				append sql_acc $token
			}
		}

		# There may be a trailing nonterminated statement; execute it
		# if so
		$self Execute $sql_acc

		event generate $win <<Executed>>
	}

	# Clear out our tabs, and close any statements that may be open on our
	# database. (Maybe don't pass database handles to this widget that you
	# don't want getting randomly stripped of all their open statements.)
	method clear {} {
		set rs_serial 0
		foreach tab [$hull tabs] {
			destroy $tab
		}
		foreach statement [[$self cget -db] statements] {
			$statement close
		}
	}

	# Execute a single SQL statement and add a rstable widget over its
	# result set to the notebook (unless it's a null statement, then smile
	# tolerantly and pretend nothing happened)
	method Execute {sql} {
		# Some drivers dislike null statements
		if {[string trim $sql] ne {}} {
			set statement [[$self cget -db] prepare $sql]
			$hull add [
				rstable $win.[::cargocult::gensym rs] -rs [
					$statement execute [$self cget -params]
				] -chunksize [$self cget -chunksize]
			] -text "Query [incr rs_serial]"
		}
	}
}

} ;# namespace eval dbluejay
